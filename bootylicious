#!/usr/bin/env perl

use FindBin;
use lib "$FindBin::Bin/lib";
use lib "$FindBin::Bin/mojo/lib";

use Mojolicious::Lite;

push @{app->plugins->namespaces}, 'Bootylicious::Plugin';

plugin 'booty_config' => {file => app->home->rel_file('bootylicious.conf')};
plugin 'booty_helpers';
plugin 'markdown_parser';
plugin 'model';
plugin 'http_cache';

my $ALIAS_RE = qr/[a-zA-Z0-9-_]+/;
my $TAG_RE   = qr/[a-zA-Z0-9]+/;

our $VERSION = '1.000000';

under sub {
    my $self = shift;

    my $path = $self->req->url->path->to_string;
    return 1 unless $path;

    return 1 if $path =~ m{/$};

    return 1 if $path =~ m{\.(?:[a-z]+)$};

    my $canonical_location =
      $self->req->url->clone->path($path . '.html')->to_abs;

    $self->app->log->debug("Path is not canonical: " . $self->req->url);
    $self->app->log->debug("Redirecting to: " . $canonical_location);

    $self->redirect_to($canonical_location);

    # Stop
    return 0;
};

get '/'      => \&index => 'root';
get '/index' => \&index => 'index';
sub index {
    my $self = shift;

    my $timestamp = $self->param('timestamp');

    my $pager = $self->get_articles(timestamp => $timestamp);

    $self->stash(booty => $pager->articles, pager => $pager);

    $self->render_smart('index');
};

get '/archive' => sub {
    my $self = shift;

    my $archive = $self->get_archive;

    $self->stash(booty => $archive);

    $self->render_smart;
} => 'archive';

get '/tags/:tag' => [tag => $TAG_RE] => sub {
    my $self = shift;

    my $tag = $self->stash('tag');

    my $timestamp = $self->param('timestamp');

    my $pager = $self->get_articles_by_tag($tag, timestamp => $timestamp);

    return $self->render_not_found unless $pager->articles->size;

    $self->stash(booty => $pager->articles, pager => $pager);

    $self->render_smart;
} => 'tag';

get '/tags' => sub {
    my $self = shift;

    my $config  = $self->config;
    my $parsers = $self->parsers;

    my $cloud = $self->get_tag_cloud;

    $self->stash(booty => $cloud);

    $self->render_smart;
} => 'tags';

get '/articles/:year/:month/:alias' =>
  [year => qr/\d+/, month => qr/\d+/, alias => $ALIAS_RE] => sub {
    my $self = shift;

    my $year  = $self->stash('year');
    my $month = $self->stash('month');
    my $name  = $self->stash('alias');

    my $article = $self->get_article($year, $month, $name);

    return $self->render_not_found unless $article;

    $self->stash(
        booty => $article,
        prev  => $article->prev,
        next  => $article->next
    );

    $self->render_smart;
  } => 'article';

get '/pages/:name' => [name => $ALIAS_RE] => sub {
    my $self = shift;

    my $name = $self->stash('name');

    my $page = $self->get_page($name);

    return $self->render_not_found unless $page;

    $self->stash(booty => $page);

    $self->render_smart;
} => 'page';

get '/drafts/:name' => [name => $ALIAS_RE] => sub {
    my $self = shift;

    my $name = $self->stash('name');

    my $draft = $self->get_draft($name);

    return $self->render_not_found unless $draft;

    $self->stash(booty => $draft);

    $self->render_smart;
} => 'draft';

app->start;

1;

__DATA__

@@ index.html.ep
% stash description => config('descr');
% my $articles = stash 'booty';
% if ($articles->size == 0) {
    <div class="text center">
        Nothing here yet :(
    </div>
% }
% while (my $article = $articles->next) {
    %= include 'index-item', article => $article;
% }
%= include 'index-pager', pager => $pager;


@@ index-item.html.ep
    <div class="text">
        <h1 class="title">
            <%= link_to_article $article %>
        </h1>
        <div class="created"><%= date $article->created %></div>
        <%= tags_links $article %>
%   if ($article->preview) {
        <%== $article->preview %>
        <div class="more"> &rarr;
            <%= link_to_full_content $article %>
        </div>
%   }
%   else {
        <%== $article->content %>
%   }
    </div>


@@ index-pager.html.ep
    <div id="pager">
        <%= link_to_page 'index', $pager->prev_timestamp => {%><span class="arrow">&larr; </span><%= strings 'later' %><%}%>
        <%= link_to_page 'index', $pager->next_timestamp => {%><%= strings 'earlier' %><span class="arrow"> &rarr;</span><%}%>
    </div>


@@ archive.html.ep
% stash title => strings('archive'), description => strings('archive-description');
% my $archive = stash 'booty';

<div class="text">
    <h1><%= strings 'archive' %></h1>
    <br />
    % while (my $year = $archive->next) {
    <h2><%= $year->{year} %></h2>
    <ul>
        % while (my $article = $year->{articles}->next) {
        <li>
            <%= link_to_article $article %>
            <br />
            <div class="created"><%= date $article->created %></div>
        </li>
        % }
    </ul>
    % }
</div>


@@ index.rss.ep
% my $articles = stash 'booty';
    <channel>
        <title><%= config 'title' %></title>
        <link><%= url_for('index')->to_abs %></link>
        <description><%= config 'descr' %></description>
        <pubDate><%= date_rss $pager->articles->last_created %></pubDate>
        <lastBuildDate><%= date_rss $pager->articles->last_created %></lastBuildDate>
        <generator>Mojolicious::Lite</generator>
        % while (my $article = $pager->articles->next) {
            % my $link = href_to_article $article;
        <item>
          <title><%== $article->title %></title>
          <link><%= $link %></link>
          <description>
            <%= $article->preview || $article->content %>
            % if ($article->link) {
                <%= permalink_to($article->link) if $article->link %>
            % }
          </description>
          % foreach my $tag (@{$article->tags}) {
          <category><%= $tag %></category>
          % }
          <pubDate><%= date_rss $article->created %></pubDate>
          <guid><%= $link %></guid>
        </item>
        % }
    </channel>

@@ layouts/wrapper.rss.ep
<?xml version="1.0" encoding="UTF-8"?>
<rss version="2.0" xml:base="<%= url_for('index')->to_abs %>"
    xmlns:dc="http://purl.org/dc/elements/1.1/">
    <%= content %>
</rss>


@@ tags.html.ep
% stash title => strings('tags'), description => strings('tags-description');
% my $cloud = stash 'booty';
<div class="text">
    <h1><%= strings 'tags' %></h1>
    <br />
    <div class="tags">
% while (my $tag = $cloud->next) {
        <%= link_to_tag $tag->{name} %>
        <sub>(<%= $tag->{count} %>)</sub>
% }
    </div>
</div>


@@ tag.html.ep
% stash title => $tag, description => strings('tag-description', $tag);
% my $articles = stash 'booty';
<div class="text">
<h1><%= strings 'tag' %> <%= $tag %>
<sup>
<%= link_to_tag $tag => { format => 'rss'} => {%><img src="/rss.png" alt="RSS" /><%}%></sup>
</h1>
<br />
% while (my $article = $articles->next) {
    <%= link_to_article $article %>
    <br />
    <div class="created"><%= date $article->created %></div>
% }
</div>
%= include 'tag-pager', pager => $pager, tag => $tag;


@@ tag-pager.html.ep
    <div id="pager">
        <%= link_to_page 'tag' => {tag => $tag} => $pager->prev_timestamp => {%><span class="arrow">&larr; </span><%= strings 'later' %><%}%>
        <%= link_to_page 'tag' => {tag => $tag} => $pager->next_timestamp => {%><%= strings 'earlier' %><span class="arrow"> &rarr;</span><%}%>
    </div>


@@ tag.rss.ep
%= include 'index', format => 'rss';


@@ article.html.ep
% my $article = stash 'booty';
% stash title => $article->title, description => $article->description;
<div class="text">
    <h1 class="title"><%= link_to_article $article %></h1>
    <div class="created"><%= date $article->created %>
%   if ($article->is_modified) {
, modified <span class="modified"><%= date $article->modified %></span>
% }
    </div>
    <%= tags_links $article %>
<%== $article->content %>
%= include 'article-pager', next => $next, prev => $prev;
</div>


@@ article-pager.html.ep
    <div id="pager">
%   if ($prev) {
        &larr; <%= link_to_article $prev %> &nbsp;|
%   }
        <a href="<%= url_for 'archive' %>"><%= strings('archive') %></a>
%   if ($next) {
        |&nbsp;<%= link_to_article $next %> &rarr;
%   }
    </div>


@@ page.html.ep
% stash title => $page->title, description => $page->description;
% my $page = stash 'booty';
<div class="text">
    <h1 class="title">
        <%= $page->title %>
    </h1>
    <%== $page->content %>
</div>


@@ draft.html.ep
% stash title => $draft->{title}, description => strings('draft');
% my $draft = stash 'booty';
<div class="text">
    <h1 class="title">
        <%= $draft->{title} %>
    </h1>
    <%= tags_links $draft %>
    <%== $draft->{content} %>
</div>


@@ not_found.html.ep
% stash title => 'Not found', description => 'Not found';
<div class="error">
    <h1>404</h1>
    <br />
    <%= strings 'not-found' %>
</div>


@@ exception.html.ep
% stash title => 'Not found', description => 'Not found';
<div class="error">
    <h1>500</h1>
    <br />
    <%= strings 'error' %>
</div>


@@ layouts/wrapper.html.ep
<!doctype html>
    <head>
        <meta charset="UTF-8">
        <title><%= $title ? "$title / " : '' %><%= config 'title' %></title>
% if ($description) {
        <meta name="description" content="<%= $description %>" />
% }
% foreach my $meta (@{config('meta')}) {
        <meta 
% for my $key (keys %$meta) {
<%== "$key=\"$meta->{$key}\" " %>
% }
/>
% }
% foreach my $file (@{config('css')}) {
        <link rel="stylesheet" href="/<%= $file %>" type="text/css" />
% }
% if (!@{config('css')}) {
        <link rel="stylesheet" href="/styles.css" type="text/css" />
% }
        <link rel="alternate" type="application/rss+xml" title="<%= config 'title' %>" href="<%= url_for('index', format => 'rss')->to_abs %>" />
    </head>
    <body>
        <div id="body">
            <div id="header">
                <h1 id="title">
                <%= link_to 'index', {format => undef} => {%><%= config 'title' %><%}%>
                <sup><a href="<%= url_for 'index', format => 'rss' %>"><img src="/rss.png" alt="RSS" /></a></sup>
                </h1>
                <h2 id="descr"><%= config 'descr' %></h2>
                <span id="author"><%= config 'author' %></span>, <span id="about"><%= config 'about' %></span>
                <div id="menu">
% for (my $i = 0; $i < @{config('menu')}; $i += 2) {
                    <a href="<%= config('menu')->[$i + 1] %>"><%== config('menu')->[$i] %></a>
% }
                </div>
            </div>
            <div id="content">
            <%= content %>
            </div>
            <div class="push"></div>
        </div>
        <div id="footer"><%== config 'footer' %></div>
% foreach my $file (@{config('js')}) {
        <script type="text/javascript" href="/<%= $file %>" />
% }
    </body>
</html>


@@ styles.css
html, body {height: 100%;margin:0}
body {background: #fff;font-family: "Helvetica Neue", Arial, Helvetica, sans-serif;}
h1,h2,h3,h4,h5 {font-family: times, "Times New Roman", times-roman, georgia, serif; line-height: 40px; letter-spacing: -1px; color: #444; margin: 0 0 0 0; padding: 0 0 0 0; font-weight: 100;}
a,a:active {color:#555}
a:hover{color:#000}
a:visited{color:#000}
img{border:0px}
pre{border:2px solid #ccc;background:#eee;padding:2em;overflow:auto;overflow-y:visible;width:600px;}
#body {width:65%;min-height:100%;height:auto !important;height:100%;margin:0 auto -6em;}
#header {text-align:center;padding:2em 0em 0.5em 0em;border-bottom: 1px solid #000}
h1#title{font-size:3em}
h2#descr{font-size:1.5em;color:#999}
span#author {font-weight:bold}
span#about {font-style:italic}
#menu {padding-top:1em;text-align:right}
#content {background:#FFFFFF}
.created, .modified {color:#999;margin-left:10px;font-size:small;font-style:italic;padding-bottom:0.5em}
.modified {margin:0px}
.tags{margin-left:10px;text-transform:uppercase;}
.text {padding:2em;}
.text h1.title {font-size:2.5em}
.error {padding:2em;text-align:center}
.more {margin-left:10px}
#pager {text-align:center;padding:2em; color:#ccc}}
#pager span.active {color:#000}
#pager span.arrow {background:#fff}
#subfooter {padding:2em;border-top:#000000 1px solid}
#footer{width:65%;margin:auto;font-size:80%;text-align:center;padding:2em 0em 2em 0em;border-top:#000000 1px solid;height:2em;}
.center {text-align:center}
.push {height:6em}


@@ rss.png (base64)
iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAAGXRFWHRTb2Z0d2FyZQBBZG9iZSBJ
bWFnZVJlYWR5ccllPAAAAlJJREFUeNqkU0toU0EUPfPJtOZDm9gSPzWVKloXgiCCInXTRTZVQcSN
LtyF6qILFwoVV+7EjR9oFy7VlSAVF+ouqMWWqCCIrbYSosaARNGmSV7ee+OdyUsMogtx4HBn5t1z
7twz85jWGv8zZHaUmRjlHBnBkRYSCSnog/wzuECZMzxgDNPEW5E0ASHTl4qf6h+KD6iwUpwyuRCw
kcCCNSPoRsNZKeS31D8WTOHLkqoagbQhV+sV1fDqEJQoidSCCMiMjskZU9HU4AAJpJsC0gokTGVD
XnfhA0DRL7+Hn38M/foOeOUzOJEZs+2Cqy5F1iXs3PZLYEGl+ux1NF7eAmpfIXedQOjYbYgdh9tk
Y3oTsDAnNCewPZqF8/SKjdqs+7aCj5wFDkwSlUEvzFgyPK8twNvuBv3GzixgzfgcQmNXqW/68IgE
is+BvRPQ0fXE9eC7Lvy/Cfi5G8DSQ7DkTrCxKbrgJPSTS5TUDQwfgWvIBO0Dvv+bgPFAz12Dzl4E
7p5svpQ9p6HLy9DFF2CD+9sCHpG9DgHHeGAExDglZnLAj09APgts2N089pdFsPjmXwIuHAJk8JKL
rXtuDWtWtQwWiliScFapQJedKxKsVFA0KezVUeMvprcfHDkua6uRzqsylQ2hE2ZPqXAld+/tTfIg
I56VgNG1SDkuhmIb+3tELCLRTYYpRdVDFpwgCJL2fJfXFufLS4Xl6v3z7zBvXkdqUxjJc8M4tC2C
fdDoNe62XPaCaOEBVOjbm++YnSphpuSiZAR6CFQS4h//ZJJD7acAAwCdOg/D5ZiZiQAAAABJRU5E
rkJggg==

__END__

=head1 NAME

Bootylicious -- one-file blog on Mojo steroids!

=head1 SYNOPSIS

    $ bootylicious daemon

=head1 DESCRIPTION

Bootylicious is a minimalistic blogging application built on top of
L<Mojolicious::Lite>. You start with just one file, but it is easily extendable
when you add new plugins, templates, css files etc.

=head1 FEATURES

=over

    * filesystem-based storage
    * tags
    * RSS (articles and by tag)
    * paging
    * static pages
    * drafts
    * themes
    * multi-parser support (POD, Markdown)
    * plugins

=back

=head1 CONFIGURATION

Bootylicious can be configured through config file that is placed in the same
directory as C<bootylicious> (or set via BOOTYLICIOUS_HOME env variable) file
and is called C<bootylicious.json>. It is in JSON format.

    {
        "title" : "my blog title",
        "descr" : "my blog description",
        "author" : "myname",
        "menu" : [
            "item1", "link1",
            "item2", "link2",
            ...
        ],
        ...
    }

If you are using Bootylicious' default templates, there are a number of
configuration options you can set to customize them without ever having to
touch html or template files:

=over 4

=item * author - set the blog author's name. Can also be set via the
BOOTYLICIOUS_AUTHOR environment variable. Default value is "whoami".

=item * about - set the blog author's short description. Can also be set
via the BOOTYLICIOUS_ABOUT environment valiable. Default value is
"Perl hacker".

=item * email - set the blog author's email. Can also be set via the
BOOTYLICIOUS_EMAIL environment variable. Default value is "" (empty).

=item * title - set the blog title. Can also be set via the BOOTYLICIOUS_TITLE
environment variable. Default value is "Just another blog".

=item * descr - set the blog's short description (subtitle). Can also be set
via the BOOTYLICIOUS_DESCR environment variable. Default value is "I do not
know if I need this".

=item * menu - set the blog's menu content. Value should be an array, because
the order matters:

    "menu" : [
        "item1", "link1",
        "item2", "link2",
        ...
    ],

=item * css - loads given css filename from BOOTYLICIOUS_PUBLICDIR/ (see below)
and uses it instead of standard bootylicious css. To load more than one css
file, in the same given order, just pass an array (e.g.:
"css" : [ "foo.css", "bar.css", "baz.css" ]).

=item * js - loads given javascript filename from BOOTYLICIOUS_PUBLICDIR/ (see
below) and uses it on the templates. To load more than one js file, in the same
given order, just pass an array (e.g.: "js" : [ "foo.js", "bar.js", "baz.js" ]).

=item * theme - bootylicious can automatically import css and js files via
themes. Just put those files under PUBLICDIR/bootylicious/themes/my-theme/ and
set this option to "my-theme". Files are loaded in the same order as the
filesystem gives them, usually alphabetic.

=item * footer - sets each page's footer text, to appear in every page.

=back

Also, the following options can be set to change the way bootylicious behaves:

=over 4

=item * articlesdir - set the dir where articles should be fetched from
Can also be set via the BOOTYLICIOUS_ARTICLESDIR environment variable
Default value is "articles".

=item * publicdir - set the dir where bootylicious looks for static objects,
like images, css/js files, etc. Can also be set via the BOOTYLICIOUS_PUBLICDIR
environment variable. Default value is "public".

=item * templatesdir - set the dir where bootylicious looks for template files,
in case you want to override the default ones. Can also be set via the
BOOTYLICIOUS_TEMPLATESDIR environment variable. Default value is "templates".

=item * pagesdir - set the dir where bootylicious looks for static pages.
Default is 'pages'.

=item * draftsdir - set the dir where bootylicious looks for draft pages.
Default is 'drafts'.

=item * cuttag - set the cuttag for parsing the articles. Default is "[cut]".

=item * cuttext - set the link to full article view for articles with a cuttag.
Default is 'Keep reading'.

=item * perl5lib - set any additional lib folders the script should look
into before trying to load Perl 5 modules (ideal for integrating with
L<< local::lib >> and use inside shared hosting environments)

=item * pagelimit - how many articles to show on index page. Default is 10.

=item * meta - html meta tags configuration. Empty by default.

=item * template_handler - what template engine to use and what template files to
search while rendering pages. Default value is 'ep'.

=item * datefmt - date formatting template (strftime). Default value is
'%a, %d %b %Y';

=back

=head1 FILESYSTEM

=head2 ARTICLES

All the articles must be placed under the articlesdir with a name like
20090730-my-new-article.EXTENSION. Based on EXTENSION they are parsed by
different parsers. See parsers section for more information.

The filename format must comply with either of the following:

=over 4

=item * YYYYMMDD-title.EXTENSION

=item * YYYYMMDDTHH:MM:SS-title.EXTENSION

=back

The title may contain dots (".") or dashes ("-") freely.

=head2 PAGES

These are static pages that don't appear on articles page and can be used to
show some static information like documentation, download area, author info etc.

=head2 DRAFTS

These are future articles that you are working on. Just place your drafts under
the draftsdir and keep working. You can look at the preview by pointing your
browser to the draft url. Noone is going to see it, because only you know the
article's title.

=head1 PLUGINS

Bootylicious can be extended by using L<Mojolicious::Plugin> derived third party
plugins.

=head2 CONFIGURATION

Configuration is done in bootylicious config file. Parameters are passed when
loading a plugin.

    # Without params (or with default ones)
    "plugins" : [
        "search",
        "gallery"
    ]

    # With params
    "plugins" : [
        "search", {
            "before_context" : 10
        },
        "gallery", {
            "columns" : 3
        }
    ]

See L<Mojolicious::Plugin> documentation for more details and
L<Bootylicious::Plugin::Search> as an example plugin.

=head1 TEMPLATES

Embedded templates will work just fine, but when you want to have something more
advanced just create a template in templates/ directory with the same name but
optionally with a different extension.

For example there is index.html.ep, thus templates/index.html.ep should be
created with a new content. If you want to use a different base directory for the 
templates, set the C<templatesdir> config option as explained above.

=head1 SUPPORT

=head2 Web

    http://getbootylicious.org/

=head1 DEVELOPMENT

=head2 Repository

    http://github.com/vti/bootylicious/commits/master

=head1 SEE ALSO

L<Mojo> L<Mojolicious> L<Mojolicious::Lite>

=head1 CREDITS

Breno G. de Oliveira

Johannes 'fish' Ziemke

Konstantin Kapitanov

Mirko Westermeier

Sebastian Riedel

Slavik Komarov

=head1 AUTHOR

Viacheslav Tykhanovskyi, C<viacheslav.t@gmail.com>.

=head1 COPYRIGHT

Copyright (C) 2008-2010, Viacheslav Tykhanovskyi.

This program is free software, you can redistribute it and/or modify it under
the same terms as Perl 5.10.

=cut
